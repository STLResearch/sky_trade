plugins {
    id "com.android.application"
    id 'com.google.gms.google-services'
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '0.0.1'
}
def keyProperties = new Properties()
def keyPropertiesFile = rootProject.file('key.properties')
if (keyPropertiesFile.exists()) {
    keyProperties.load(new FileInputStream(keyPropertiesFile))
}

android {
    namespace "mobile.trade.sky"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "mobile.trade.sky"
        minSdkVersion 26
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keyProperties['keyAlias']
            keyPassword keyProperties['keyPassword']
            storeFile file(keyProperties['storeFile'])
            storePassword keyProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            versionNameSuffix ".dev"
            dimension "environment"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            manifestPlaceholders += [
                    host       : "dev.sky.trade",
                    auth0Domain: "dev-auth.sky.trade",
                    auth0Scheme: "https"
            ]
            signingConfig signingConfigs.debug
        }
        stage {
            versionNameSuffix ".stage"
            dimension "environment"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            manifestPlaceholders += [
                    host       : "staging.sky.trade",
                    auth0Domain: "staging-auth.sky.trade",
                    auth0Scheme: "https"
            ]
            signingConfig signingConfigs.release
        }
        live {
            versionNameSuffix ".live"
            dimension "environment"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            manifestPlaceholders += [
                    host       : "app.sky.trade",
                    auth0Domain: "auth.sky.trade",
                    auth0Scheme: "https"
            ]
            signingConfig signingConfigs.release
        }
    }

}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
}
