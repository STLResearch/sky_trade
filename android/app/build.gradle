plugins {
    id "com.android.application"
    id 'com.google.gms.google-services'
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '0.0.1'
}
def keyProperties = new Properties()
def keyPropertiesFile = rootProject.file('key.properties')
if (keyPropertiesFile.exists()) {
    keyProperties.load(new FileInputStream(keyPropertiesFile))
}

android {
    namespace "mobile.trade.sky"
    compileSdk flutter.compileSdkVersion
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        debug {
            keyAlias keyProperties['debugKeyAlias']
            keyPassword keyProperties['debugKeyPassword']
            storeFile file(keyProperties['debugStoreFile'])
            storePassword keyProperties['debugStorePassword']
        }
        release {
            keyAlias keyProperties['keyAlias']
            keyPassword keyProperties['keyPassword']
            storeFile file(keyProperties['storeFile'])
            storePassword keyProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        profile {
            signingConfig signingConfigs.release
        }
        release {
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            versionNameSuffix "-dev"
            applicationId "mobile.trade.sky.dev"
            dimension "environment"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            resValue "string", "app_name", "SkyTrade (DEV)"
            manifestPlaceholders += [
                    deepLinkHost             : "dev.sky.trade",
                    emailVerificationLinkPath: "/u/email-verification",
                    auth0Domain              : "dev-auth.sky.trade",
                    auth0Scheme              : "https"
            ]
        }
        stage {
            versionNameSuffix "-stage"
            applicationId "mobile.trade.sky.stage"
            dimension "environment"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            resValue "string", "app_name", "SkyTrade (STAGE)"
            manifestPlaceholders += [
                    deepLinkHost             : "staging.sky.trade",
                    emailVerificationLinkPath: "/u/email-verification",
                    auth0Domain              : "staging-auth.sky.trade",
                    auth0Scheme              : "https"
            ]
        }
        live {
            versionNameSuffix "-live"
            applicationId "mobile.trade.sky"
            dimension "environment"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            resValue "string", "app_name", "SkyTrade"
            manifestPlaceholders += [
                    deepLinkHost             : "app.sky.trade",
                    emailVerificationLinkPath: "/u/email-verification",
                    auth0Domain              : "auth.sky.trade",
                    auth0Scheme              : "https"
            ]
        }
    }

}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
}
